/*
 * Copyright (c) 2014, the authors.
 *
 *   This file is part of 'DROPS'.
 *
 *   DROPS is free software: you can redistribute it and/or modify
 *   it under the terms of the GNU General Public License as published by
 *   the Free Software Foundation, either version 3 of the License, or
 *   (at your option) any later version.
 *
 *   DROPS is distributed in the hope that it will be useful,
 *   but WITHOUT ANY WARRANTY; without even the implied warranty of
 *   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *   GNU General Public License for more details.
 *
 *   You should have received a copy of the GNU General Public License
 *   along with DROPS.  If not, see <http://www.gnu.org/licenses/>.
 */


apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven-publish'
sourceCompatibility = 1.6
version = '0.1.0'

ext {
    bintrayBaseUrl = 'https://api.bintray.com/maven'
    bintrayUsername = 'paolo-ditommaso'
    bintrayRepository = 'maven'
    bintrayPackage = 'drops'
}


repositories {
    flatDir(dirs: file('lib'))
    mavenLocal()
    mavenCentral()
    maven { url 'http://uk.maven.org/maven2' }
    maven { url 'http://dl.bintray.com/paolo-ditommaso/maven'}
}


dependencies {

    // test dependencies
    compile ("org.codehaus.groovy:groovy:${groovyVer}")
    compile "org.slf4j:slf4j-api:${slf4jVer}"

    testCompile ("cglib:cglib-nodep:${cglibVer}")
    testCompile ("org.objenesis:objenesis:${objenesisVer}")
    testCompile ("org.spockframework:spock-core:${spockVer}") {
        exclude module: 'groovy-all'
    }

}


task compile(type: Copy) {
    dependsOn compileJava, compileGroovy, jar

    into "$buildDir/dependency-libs"
    from configurations.runtime
}


task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

assemble.dependsOn sourcesJar, javadocJar

publishing {
    publications {
        plugin(MavenPublication) {
            groupId 'nextflow'
            artifactId 'drops'
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
/*
    repositories {
        maven {
            name 'Bintray'
            url "$bintrayBaseUrl/$bintrayUsername/$bintrayRepository/$bintrayPackage"

            credentials {
                username = bintrayUsername
                password = bintrayApiKey
            }
        }
    }
*/
}